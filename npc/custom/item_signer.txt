//===== rAthena Script =======================================
//= Sign Your Items
//===== By: ==================================================
//= Lupus
//===== Current Version: =====================================
//= 1.1a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Write you name on your rare equipment or weapon ^_-
//===== Additional Comments: =================================
//= 1.1 Cleaned and edited for general use. [Euphy]
//= 1.1a Added 'disable_items' command. [Euphy]
//============================================================
//= Traduzido por Alek Emundus

prontera,147,174,7	script	Perchik	47,{
	
	setarray .@Item[0],0,0;	// Item requirements: <ID>,<Count>{,...} (0 to disable)
	setarray .@Cost[0],0,5000;	// Zeny requirements: <base price>,<price per refine>

	disable_items;
	mes "[Perchik]";
	mes "Eu posso ^0055FFassinar seu nome^000000 em quase todos os itens raros que voce possui.";
	next;
	if(select("Me fale mais...:Assine meus itens, por favor!") == 1) {
		mes "[Perchik]";
		mes "Posso colocar seu nome em qualquer equipamento ou arma sem slot.";
		emotion ET_AHA;
		next;
		mes "[Perchik]";
		if (getarraysize(.@Item) || getarraysize(.@Cost)) {
			mes "Para meu trabalho eu aceito:";
			if (getarraysize(.@Item)) for(set .@i,0; .@i<getarraysize(.@Item); set .@i,.@i+2)
				mes " ~ "+.@Item[.@i+1]+"x "+getitemname(.@Item[.@i]);
			if (.@Cost[0]) mes " ~ "+callfunc("F_InsertComma",.@Cost[0])+" Zeny";
			if (.@Cost[1]) mes " ~ "+callfunc("F_InsertComma",.@Cost[1])+" Zeny por refino";
		}
		else mes "Eu vou fazer de graca, mas...";
		next;
		emotion ET_CRY;
		mes "[Perchik]";
		mes "Infelizmente, tenho 12 filhos famintos";
		mes "e uma esposa muito brava.";
		next;
		mes "[Perchik]";
		mes "Ou sao 12 filhos bravos";
		mes "e uma esposa faminta...";
		emotion ET_SCRATCH;
		close;
	}
	mes "[Perchik]";
	mes "Me mostre seus itens para assinar...";
	next;
	setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
	for (set .@i,1; .@i<=10; set .@i,.@i+1) {
		if (getequipisequiped(.@indices[.@i]))
			set .@menu$, .@menu$ + F_getpositionname(.@indices[.@i]) +" [^0055FF"+getequipname(.@indices[.@i])+"^000000]";
		set .@menu$, .@menu$+":";
	}
	set .@part, .@indices[ select(.@menu$) ];
	set .@id, getequipid(.@part);
	set .@ref, getequiprefinerycnt(.@part);
	mes "[Perchik]";
	if (!getequipisequiped(.@part)) {
		mes "Nada esta equipado ali!";
		emotion ET_KEK;
		close;
	}
	for(set .@i,0; .@i<4; set .@i,.@i+1)
		set .@slot[.@i], getequipcardid(.@part,.@i);
	if (.@slot[0]==255 || .@slot[0]==254 || .@slot[0]<0) {
		mes "Infelizmente esse item ja esta assinado.";
		mes "Nao posso tocar num trabalho ja feito.";
		emotion ET_SCRATCH;
		close;
	}
	if (.@slot[0]>4000 && .@slot[0]<5000) {
		mes "Uma carta? Aqui?!";
		mes "Como eu disse antes, nao assino itens com cartas.";
		emotion ET_SCRATCH;
		close;
	}
	if (getiteminfo(.@id, ITEMINFO_SLOT)) {
		mes "Desculpe, nao assino itens com slots.";
		emotion ET_SORRY;
		close;
	}
	getinventorylist;
	for(set .@i,0; .@i<@inventorylist_count; set .@i,.@i+1)
		if (@inventorylist_expire[.@i] != 0) {
			mes "Desculpe, nao assino itens alugados!";
			emotion ET_SCRATCH;
			close;
		}
	set .@price, .@Cost[0]+(.@Cost[1]*.@ref);
	if (getarraysize(.@Item) || .@price) {
		mes "Eu vou precisar de:";
		if (getarraysize(.@Item)) for(set .@i,0; .@i<getarraysize(.@Item); set .@i,.@i+2) {
			mes " ~ "+.@Item[.@i+1]+"x "+getitemname(.@Item[.@i]);
			if (countitem(.@Item[.@i]) < .@Item[.@i+1]) set .@nr,1;
		}
		if (.@price) mes " ~ "+.@price+" Zeny";
	}
	mes "Posso assinar  ^0055FF"+getitemname(.@id)+"^000000?";
	next;
	if(select("Ok!:Sair")==2) {
		mes "[Perchik]";
		mes "Ate mais...";
		emotion ET_SLEEPY;
		close;
	}
	mes "[Perchik]";
	if (Zeny < .@price || .@nr) {
		mes "Eu nao trabalho por 'obrigado'.";
		emotion ET_SORRY;
		close;
	}
	if (!countitem2(.@id,1,.@ref,0,.@slot[0],.@slot[1],.@slot[2],.@slot[3])) {
		mes "Onde esta "+getitemname(@id)+"...?";
		npctalk "Perchik : Voce e um trapaceiro dormente!";
		logmes "Hack: Tentativa de assinar um item sem ter: "+getitemname(@id);
		emotion ET_KEK;
		close;
	}
	if (.@price) set Zeny, Zeny-.@price;
	if (getarraysize(.@Item))  for(set .@i,0; .@i<getarraysize(.@Item); set .@i,.@i+2)
		delitem .@Item[.@i], .@Item[.@i+1];
	delitem2 .@id,1,1,.@ref,0,.@slot[0],.@slot[1],.@slot[2],.@slot[3];
	mes "Feito!";
	emotion ET_OK;
	getitem2 .@id,1,1,.@ref,0,254,0,getcharid(0)&0xffff,(getcharid(0)>>16)&0xffff;
	equip .@id;
	close;
}
